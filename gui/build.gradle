plugins {
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'maven-publish'
}
repositories {
    mavenCentral()
}

def versions = [
        kafka     : project.properties['kafka.version'] ?: "3.3.1",
        junit     : project.properties['junit.version'] ?: "5.9.1",
        jcommander: project.properties['jcommander.version'] ?: "1.82",
]

java {
    sourceCompatibility = 11
    targetCompatibility = 11
}

javafx {
    version = "17"
    modules = ['javafx.controls']
}

ext {
    numberOfForks = project.hasProperty('maxParallelForks') ? maxParallelForks.toInteger() : Math.max((int) (Runtime.runtime.availableProcessors() / 2), 1)
}

archivesBaseName = "astraea-gui"
jar {
    manifest {
        attributes 'Main-Class': 'org.astraea.gui.Main'
    }
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter:${versions["junit"]}"
    testImplementation project(':it')
    implementation project(':common')
    implementation "org.apache.kafka:kafka-clients:${versions["kafka"]}"
}

publishing {
    publications {
        binary(MavenPublication) {
            groupId = 'org.astraea'
            artifactId = 'astraea-gui'
            artifact shadowJar
        }
    }
    repositories {
        maven {
            url = "https://maven.pkg.github.com/skiptests/astraea"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()

    maxParallelForks = numberOfForks
    // make isolation for tests. It may be expensive but stability is first choice.
    forkEvery = 1
    testLogging {
        events "PASSED", "STARTED", "FAILED", "SKIPPED"
        exceptionFormat = 'full'
    }

    minHeapSize = "512m"
    maxHeapSize = "2048m"
}