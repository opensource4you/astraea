plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

def versions = [
    kafka: project.properties['kafka.version'] ?: "3.1.0",
    junit: project.properties['junit.version'] ?: "5.8.2",
    mockito: project.properties['mockito.version'] ?: "4.2.0",
    jcommander: project.properties['jcommander.version'] ?: "1.81",
    slf4j: project.properties['slf4j.version'] ?: "1.7.32",
    zookeeper: project.properties['zookeeper.version'] ?: "3.6.3",
    json: project.properties['json.version']?: "20090211",
    yaml: project.properties['yaml.version']?: "1.8",
    lombok: project.properties['lombok.version']?: "1.18.22",
    gson: project.properties['gson.version']?: "2.7"


]

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter:${versions["junit"]}"
    testImplementation "org.mockito:mockito-core:${versions["mockito"]}"
    testImplementation "org.mockito:mockito-inline:${versions["mockito"]}"
    testImplementation "org.apache.zookeeper:zookeeper:${versions["zookeeper"]}"
    testImplementation "org.apache.kafka:kafka_2.12:${versions["kafka"]}"


    implementation "org.apache.kafka:kafka-clients:${versions["kafka"]}"
    implementation "com.beust:jcommander:${versions["jcommander"]}"
    // we don't use slf4j actually, and it is used by kafka so we swallow the log.
    implementation "org.slf4j:slf4j-nop:${versions["slf4j"]}"
    implementation "org.json:json:${versions["json"]}"
    implementation "org.yaml:snakeyaml:${versions["yaml"]}"
    compileOnly "org.projectlombok:lombok:${versions["lombok"]}"
    annotationProcessor "org.projectlombok:lombok:${versions["lombok"]}"
    implementation "com.google.code.gson:gson:${versions["gson"]}"

}

application {
    // Define the main class for the application.
    mainClass = 'org.astraea.App'
    applicationDefaultJvmArgs = ["-server", "-XX:+UseG1GC", "-Djava.awt.headless=true", "-Xms4G", "-Xmx4G"]
}

java {
    sourceCompatibility = 11
    targetCompatibility = 11
}

tasks.named('test') {
    int numberOfForks = project.hasProperty('maxParallelForks') ? maxParallelForks.toInteger() : Math.max((int) (Runtime.runtime.availableProcessors() / 2), 1)

    // Use JUnit Platform for unit tests.
    useJUnitPlatform()

    maxParallelForks = numberOfForks
    // make isolation for tests. It may be expensive but stability is first choice.
    forkEvery = 1
    testLogging {
        events "PASSED", "STARTED", "FAILED", "SKIPPED"
        exceptionFormat = 'full'
    }

    minHeapSize = "512m"
    maxHeapSize = "2048m"
}
